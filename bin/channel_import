#!/usr/bin/perl

use strict;
use warnings FATAL => 'all';

use lib './lib';
use MythConverg::Schema;

use Getopt::Long;
my $videosource = 'default';
my $channelsfile = 'channels.conf';
GetOptions(
    's|videosource=s' => \$videosource,
    'c|channels=s' => \$channelsfile,
)or die "Failed to parse command line options.\n";;

use File::Slurp;
my @scanned_chans = read_file $channelsfile;

use HTTP::Lite;
my $http = HTTP::Lite->new;
my $req = $http->request(
    'http://supplement.xmltv.org/tv_grab_uk_rt/lineups/freesat.map', # FIXME
) or die "Unable to get document: $!";

my $sat_chans = {};
foreach my $line (split m/\n/, $http->body) {
    my ($cs, $sid, $xid) = split m/\|/, $line;
    next unless defined $xid and length $xid
        and $xid !~ m/RADIO|DATA|ADULT/;
    $sat_chans->{$sid} = { callsign => $cs, xmltvid => $xid };
}

my $s = MythConverg::Schema->connect(
    'dbi:mysql:database=mythconverg',
    'mythtv', 'V0yNzk77',
    { mysql_enable_utf8 => 1, quote_char => q{`}, name_sep => q{.} },  
);

my $sources = $s->resultset('Videosource');
my $channels = $s->resultset('Channel');
my $multiplexes = $s->resultset('DtvMultiplex');

my $source = $sources->find({name => $videosource})
    or die "Unknown video source '$videosource' (use -s <name>).\n";
my $sourceid = $source->sourceid;

# XXX
$multiplexes->delete;
$channels->delete;
# XXX

my $max_chanid  = $channels->get_column('chanid')->max;
my $max_channum = $channels->get_column('channum')->max;
my @grabber = ();

foreach my $line (@scanned_chans) {
    # http://linuxtv.org/vdrwiki/index.php/Syntax_of_channels.conf
    # callsign;bouquet:frequency:params(polarity,high_prio):source:symbolrate:vpid:apid:teletextpid:conditionalflag:serviceid:networkid:transportid:radioid
    # Channel 4;BSkyB:10714:hC56:S28.2E:22000:2327:2328:2329:0:9212:2:2041:0
    my ($cs,$freq,$params,undef,$sr,undef,undef,undef,undef,$sid,$nid,$tid,undef)
        = ($line =~ m/([^:]+):?/g);
    $cs =~ s/;.+//;
    (my $pol = $params) =~ s{.*([hv]).*}{$1}i;
    (my $fec = $params) =~ s{C(\d)(\d)}{$1/$2}; #Â breaks for 0
    chomp $sid;

    if (!exists $sat_chans->{$sid}) {
        print STDERR "Skipping channel '$cs' with no xmltvid\n";
        next;
    }
    $cs = $sat_chans->{$sid}->{callsign}; # override from tv_grab_uk_rt

    my $mplex = $multiplexes->find_or_create(
        {
            sourceid => $sourceid,
            frequency => ($freq < 100_000 ? $freq * 1_000 : $freq ),
            symbolrate => ($sr < 100_000 ? $sr * 1_000 : $sr),
            polarity => $pol,
        },{
            key => 'zap_key',
        },
    );

    $mplex->update({
        transportid => $tid,
        networkid => $nid,
        fec => $fec,
        mod_sys => 'DVB-S',
    });

    # does callsign exist? if so, re-use channum, else new channum
    my $has_chan = $channels->search({callsign => $cs})->count;

    my $chan = $channels->find_or_create(
        {
            chanid => ++$max_chanid,
            channum => ($has_chan
                ? $channels->search({callsign => $cs})->first->channum
                : ++$max_channum),
            sourceid => $sourceid,
        },{
            key => 'channum_sourceid_key',
        },
    );

    # allow update of meta such as xmltvid
    $chan->update({
        callsign => $cs,
        mplexid => $mplex,
        serviceid => $sid,
        xmltvid => $sat_chans->{$sid}->{xmltvid},
    });

    push @grabber, "channel $sat_chans->{$sid}->{xmltvid}\n";
}

write_file "$videosource.xmltv", @grabber;

__END__
mysql> select * from channel where callsign = 'BBC 1 South';
+--------+---------+--------+----------+-------------+-------------+------+----------+--------------+---------+-------------+----------+------------+--------+-------+----------+---------+---------------+---------------+---------+-----------+----------+-----------------+-----------------+---------------------+-------------------+------------+
| chanid | channum | freqid | sourceid | callsign    | name        | icon | finetune | videofilters | xmltvid | recpriority | contrast | brightness | colour | hue   | tvformat | visible | outputfilters | useonairguide | mplexid | serviceid | tmoffset | atsc_major_chan | atsc_minor_chan | last_record         | default_authority | commmethod |
+--------+---------+--------+----------+-------------+-------------+------+----------+--------------+---------+-------------+----------+------------+--------+-------+----------+---------+---------------+---------------+---------+-----------+----------+-----------------+-----------------+---------------------+-------------------+------------+
|      1 | 10353   | NULL   |        2 | BBC 1 South | BBC 1 South |      |     NULL |              |         |           0 |    32768 |      32768 |  32768 | 32768 |          |       1 |               |             1 |     126 |     10353 |        0 |               0 |               0 | 2011-11-06 15:10:35 | fp.bbc.co.uk      |         -1 |
+--------+---------+--------+----------+-------------+-------------+------+----------+--------------+---------+-------------+----------+------------+--------+-------+----------+---------+---------------+---------------+---------+-----------+----------+-----------------+-----------------+---------------------+-------------------+------------+
1 row in set (0.00 sec)

mysql> select * from dtv_multiplex where mplexid = 126;
+---------+----------+-------------+-----------+-----------+-----------+------------+------+----------+------------+-----------+--------------+-------------------+----------------+---------+---------------+-----------+--------------+---------+---------+------------+----------------+---------------------+-------------------+
| mplexid | sourceid | transportid | networkid | frequency | inversion | symbolrate | fec  | polarity | modulation | bandwidth | lp_code_rate | transmission_mode | guard_interval | visible | constellation | hierarchy | hp_code_rate | mod_sys | rolloff | sistandard | serviceversion | updatetimestamp     | default_authority |
+---------+----------+-------------+-----------+-----------+-----------+------------+------+----------+------------+-----------+--------------+-------------------+----------------+---------+---------------+-----------+--------------+---------+---------+------------+----------------+---------------------+-------------------+
|     126 |        2 |        2048 |         2 |  10817500 | a         |   22000000 | 5/6  | v        | qpsk       | a         | auto         | a                 | auto           |       0 | qpsk          | a         | auto         | DVB-S   | 0.35    | dvb        |             33 | 2011-11-06 13:51:16 |                   |
+---------+----------+-------------+-----------+-----------+-----------+------------+------+----------+------------+-----------+--------------+-------------------+----------------+---------+---------------+-----------+--------------+---------+---------+------------+----------------+---------------------+-------------------+
1 row in set (0.00 sec)

